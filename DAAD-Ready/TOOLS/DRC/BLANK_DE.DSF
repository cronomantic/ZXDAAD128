;-----------------------------------------------------------------------------------------
;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;ßßßßß                                                                               ßßßßß
;ßßßßß    German language template, created by Stefan Vogt, Puddle Soft [1-7-2020]   ßßßßß
;ßßßßß                                                                               ßßßßß
;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

; Bitte die ß in der obigen Box nicht entfernen, denn sie machen es einfach nachzuvollziehen, 
; ob du die Datei im falschen Encoding geöffnet hast.  Das wäre entweder ISO-8859-1 oder Win1252.
; Wenn du oben keine ß sehen kannst, stelle sicher dass dein Editor diese Encodings unterstützt 
; und anwendet.

;------------------------------------------------------------------------------
;
; 'Everything is possible in this life ( but it all costs time and money! )'
;                                                              Tim Gilberts
;
;                                      'And also WE ALL have to work for it'
;                                                            Andres Samudio
;
;                                                                   [8-6-89]
;
;------------------------------------------------------------------------------


; --- Please don't remove the following code, it makes sure the game works fine for all supported targets 

#ifdef "SPLIT"
#define splitModeON 1
#endif

#ifdef "tape48"
#define tape 1
#endif

#ifndef "tape"
#ifndef "st"
#ifndef "amiga"
#ifndef "zx128"
#ifndef "next"
    #extern "MALUVA"
#endif
#endif
#endif
#endif
#endif

#ifdef "zx128"
#define nativeraster 1
#endif

#ifdef "html"
#define nativeraster 1
#endif

#ifdef "pcw"
#define nativeraster 1
#endif

#ifdef "amiga"
#define nativeraster 1
#endif

#ifdef "pc"
#define nativeraster 1
#endif

#ifdef "st"
#define nativeraster 1
#endif

#ifdef "uno"
#int "MLV_UNO_INT.BIN"
#define canBoostSpeed 1
#endif

#ifdef "next"
#define canBoostSpeed 1
#endif

#ifdef "cpc"
#int "MLV_CPC_INT.BIN"
#endif

#ifdef "splitModeON"
#echo "La máquina destino usará split mode"
#endif

#ifdef "canBoostSpeed"
#echo "La máquina destino tiene modo turbo"
#endif

; --- Please don't remove the code above, it makes sure the game works fine for all supported targets 


; System flags 0-63
#define fDark               0
#define fObjectsCarried     1
#define fDarkF              28
#define fGFlags             29     ; This is best tested using HASAT GMODE
#define fScore              30
#define fTurns              31    ; 2 bytes
#define fTurnsHi            32

#define fVerb               33
#define fNoun               34
#define fAdject1            35
#define fAdverb             36
#define fMaxCarr            37
#define fPlayer             38
#define fStream             41
#define fPrompt             42
#define fPrep               43
#define fNoun2              44
#define fAdject2            45
#define fCPronounNoun       46
#define fCPronounAdject     47
#define fTimeout            48
#define fTimeoutFlags       49
#define fDoallObjNo         50
#define fRefObject          51
#define fStrength           52
#define fObjFlags           53
#define fRefObjLoc          54
#define fRefObjWeight       55
#define fRefObjIsContainer  56
#define fRefObjisWearable   57
#define fRefObjAttr1        58
#define fRefObjAttr2        59
#define fInkeyKey1          60
#define fInkeyKey2          61
#define fScreenMode         62     ; 2=Text, 4=CGA, 13=EGA, 141=VGA
#define fCurrentWindow      63     ; Which window is active at the moment

; Attr flags for HASAT/HASNAT
#define WEARABLE  23            ;Current object is wearable
#define CONTAINER 31            ;Current object is a container
#define LISTED    55            ;Objects - listed by LISTOBJ etc
#define OA_CLIST  54            ; - continous list
#define OO_CLIST  64
#define TIMEOUT   87            ;If Timeout last frame
#define IA_RBUF   85            ;Input - recall buffer
#define IO_RBUF   32
#define IA_PINP   84            ; - reprint in stream
#define IO_PINP   16
#define IA_CSTR   83            ; - clear stream
#define IO_CSTR   8
#define IA_TAKEY  82            ; - timeout on any key
#define IO_TAKEY  4
#define IA_TMORE  81            ; - timeout on More...
#define IO_TMORE  2
#define IA_TSTAR  80            ; - timeout at start of input
#define IO_TSTAR  1
#define GMODE     247           ;Graphics - available
#define GA_MDRW   246           ; - Invisible draw (drawstring)
#define GO_MDRW   64
#define GA_POFF   245           ; - Pictures OFF (drawstring)
#define GO_POFF   32
#define GA_WKEY   244           ; - Wait after drawing (drawstring)
#define GO_WKEY   16
#define GA_CBOR   243           ; - Change BORDER (drawstring)
#define GO_CBOR   8
#define MOUSE     240           ;mouse available (!DRAW only)


/CTL    ;Control Section (null char is an underline)
_       


;------------------------------------------------------------------------------
/VOC    ;Vokabular

;                       Verben und Nomen <14 sind fuer Bewegungen reserviert
N       2       noun
NORD    2       noun
NORDE   2       noun
S       3       noun
SUED    3       noun
SUEDE   3       noun
O       4       noun
OST     4       noun
OSTEN   4       noun
W       5       noun
WEST    5       noun
WESTE   5       noun
NO      6       noun
NW      7       noun
SO      8       noun
SW      9       noun
H       10      noun
HOCH    10      noun
OBEN    10      verb
R       11      noun
RUNTE   11      noun
UNTEN   11      verb

;                       Nomen   <20 koennen auch als Verben eingesetzt
;                               <50 keine Verwendung von Pronomen
I       14      noun
INVEN   14      noun
ALLES   20      noun
ALLE    20      noun
JEDEN   20      noun
JEDES   20      noun

;
FACKE   100     noun

;                       Verben
NIMM    20      verb
HOLE    20      verb
ABSTE   21      verb
ABLEG   21      verb
LEGE    21      verb
ENTFE   22      verb
TRAGE   23      verb
B       24      verb
BESCH   24      verb
ENDE    25      verb
STOP    25      verb
SPEIC   26      verb
LADE    27      verb
LADEN   27      verb
RAMSP   28      verb
RAMLA   29      verb
SCHAU   30      verb
BLICK   30      verb
SIEHE   30      verb
SIEH    30      verb
PRUEF   30      verb
BETRA   30      verb
SAG     31      verb
SAGE    31      verb
FRAG    31      verb
FRAGE   31      verb
SPRIC   31      verb
REDE    31      verb

;                               Adjektive
KLEIN   2       adjective
GROSS   3       adjective
ALTE    4       adjective
ALTEN   4       adjective
ALTES   4       adjective
NEUE    5       adjective
NEUEN   5       adjective
HARTE   6       adjective
WEICH   7       adjective
KURZE   8       adjective
LANGE   9       adjective

;                               Adverben
SCHNE   2       adverb
LANGS   3       adverb
RUHIG   4       adverb
LAUT    5       adverb
VORSI   6       adverb
SANFT   6       adverb
BEHUT   6       adverb

;                               Praepositionen
ZU      2       preposition
VON     3       preposition
VOM     3       preposition
IN      4       preposition
AUS     5       preposition
DURCH   6       preposition
UEBER   7       preposition
UNTER   8       preposition
MIT     9       preposition
AN      10      preposition
AB      11      preposition
BEI     12      preposition
AUSSE   13      preposition

;                               Pronomen
ES      2       pronoun
IHN     2       pronoun
SIE     2       pronoun

;                               Konjugationen
UND     2       conjugation
DANN    2       conjugation



;------------------------------------------------------------------------------
/STX    ;System Message Texts
/0 "Es ist zu dunkel um etwas zu sehen."
/1 "Außerdem erkenne ich: "
/2 "#nWas nun?"
/3 "#nWas als nächstes?"
/4 "#nWas soll ich jetzt tun?"
/5 "#nWas ist der nächste Schritt?"
/6 "#nDas habe ich nicht verstanden. Bitte versuch dich anders auszudrücken."
/7 "#nIn diese Richtung kann ich nicht gehen."
/8 "Das kann ich nicht machen.#n"
/9 "Ich habe bei mir:#n"
/10 "Ich habe angezogen:#n"
/11 "";*Spare
/12 "Bist Du sicher? "
/13 "Möchtest Du es noch einmal versuchen? "
/14 "";*Spare
/15 "OK.#n"
/16 "Drücke eine Taste um fortzufahren.#n"
/17 "";*You have taken
/18 "";*\sturn
/19 "";*s
/20 "";*.[CR]
/21 "";*You have scored
/22 "";*%[CR]
/23 "So etwas trage ich nicht.#n"
/24 "Ich kann nicht. Ich habe bereits etwas Anderes angezogen."
/25 "Diesen Gegenstand habe ich bereits."
/26 "So etwas kann ich hier nicht sehen."
/27 "Ich kann nicht noch mehr Dinge tragen."
/28 "So etwas habe ich nicht."
/29 "Das trage ich bereits."
/30 "J"    ;One upper case character only
/31 "N"    ;One upper case character only
/32 "Mehr..."
/33 "#n>"
/34 "";*Spare
/35 "#nDie Zeit vergeht...#n"
/36 "Genommen.#n"
/37 "Angezogen.#n"
/38 "Ausgezogen.#n"
/39 "Wurde auf den Boden gelegt.#n"
/40 "Das kann ich nicht anziehen.#n"
/41 "Das kann ich nicht ausziehen.#n"
/42 "Das kann ich nicht ausziehen, meine Hände sind voll.#n"
/43 "Das wiegt mir zuviel.#n"
/44 "#nDas befindet sich in "
/45 "Das ist leider nicht in "
/46 ", "
/47 " und "
/48 ".#n"
/49 "Hab ich nicht bei mir.#n"
/50 "Da trage ich nicht.#n"
/51 ".#n"
/52 "Davon kann ich nichts sehen in "
/53 "Nichts.#n"
/54 "T" ;Letter for Tape
/55 "D" ;Disc
/56 "Laufwerk nicht bereit, Taste drücken.#n"
/57 "I/O Fehler.#n"
/58 "Diskette oder Verzeichnis voll."
/59 "Ungültiger Dateiname."
/60 "Bitte Dateinamen eingeben:"
/61 "Tape starten.#n"
/62 "Tape or Diskette?"


;------------------------------------------------------------------------------
/MTX    ;Message Texts

;------------------------------------------------------------------------------
/OTX    ;Object Texts
/0 "eine Fackel (brennend)"


;------------------------------------------------------------------------------
/LTX    ;Location Texts
/0 "LEERE DATENBANK - DAAD V2."
/1 "Ich befinde mich auf einer Lichtung. Eine Straße führt nach Norden."
/2 "Ich befinde mich an einem Flusslauf. Ein Trampelpfad führt nach Süden."


;------------------------------------------------------------------------------
/CON    ;Connections
/0
/1
N 2
/2
S 1



;------------------------------------------------------------------------------
/OBJ    ;Object Definitions
;num  starts at  weight    c w  5 4 3 2 1 0 9 8 7 6 5 4 3 2 1 0    noun   adjective
/0    CARRIED       1      _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    FACKE  _


;------------------------------------------------------------------------------
/PRO 0       ;Main Location Loop

; Dieser Vorgang ist der erste, der vom DAAD durchgeführt wird, und enthält somit einen ersten Eintrag, der
; Springt zu dem Prozess, der Objekte, Flags usw. initialisiert. und dann jedes Mal, wenn er gerufen wird, und das
; Befindet sich der Spieler nicht mehr am Ort 0, ist er für die Darstellung der Grafik und des Textes des Ortes verantwortlich
; aktuell, falls zutreffend, und springen Sie zu Prozess 1, wo sich das System befindet, das den Spieler nach Befehlen fragt.

> _       _     AT      0                ; Wenn wir uns in der ersten Lokalität befinden...
                #ifdef "canBoostSpeed"
                    XSPEED  1                ; Wenn Sie die CPU-Geschwindigkeit ändern können, stellen Sie sie auf 7 MHz ein
                #endif
                #ifdef "splitModeON"    ; Wenn es den geteilten Bildschirm unterstützt, aktivieren Sie es
                    XSPLITSCR 1
                #endif
                PROCESS 6               ; Der Initialisierungsprozess


> _       _     WINDOW 1
                WINAT 14 0              ; Standardmäßig befindet sich das Textfenster unten
                WINSIZE 25 127


> _       _     WINDOW  0               ; Grafikfenster
                CLEAR   fDarkF          ; Gehen Sie davon aus, dass es Licht gibt
                NOTZERO 0
                ABSENT  0
                SET     fDarkF          ; Dunkel
                

                
#ifdef "nativeraster"               ; GRAPH FÜR ZIELE MIT NATÜRLICHER RASTERGRAFIK
> _       _     ZERO fDarkF
                PICTURE @fPlayer        ; Wenn es ein Bild gibt, laden Sie es hoch
                DISPLAY 0
                SKIP $pictureOK

#else                               ; GRAPH FÜR ZIELE MIT NICHT-NATIVER RASTERGRAFIKEN, DIE NICHT Spectrum 48K SIND
    #ifndef "tape48"                 
    > _       _     ZERO fDarkF
                    CLEAR 20
                    XPICTURE @fPlayer        ; Wenn es ein Bild gibt, laden Sie es hoch
                    ZERO 20                  ; Wenn es geladen wurde...
                    SKIP $pictureOK
    #endif
#endif

#ifndef "splitModeON"                  ; Dieser Eintrag wird nur ausgeführt, wenn kein geteilter Bildschirm vorhanden ist, und wir gelangen nur zu ihm, wenn das Laden des Bildes fehlgeschlagen ist oder es dunkel ist
> _       _     WINDOW 1
                WINAT 0 0               ; Wenn kein Bild vorhanden ist, wechseln wir zum Vollbildtext
                WINSIZE 25 127
                CLS
                #ifdef "next"
                    GT  20 127          ; Wenn das Laden des Bildes fehlschlägt...
                    XNEXTCLS           ; Deaktivieren und löschen Sie Layer2
                #endif
#else
> _ _           CLS                      ; Wenn das Laden fehlschlägt, wir aber über den Split-Modus verfügen, beschränken wir uns auf die CLS des Grafikfensters

#endif

$pictureOK

> _       _     WINDOW 1                 ; Textfenster
                NOTZERO fDarkF           ; Dunkel
                SYSMESS 0

> _       _     ZERO    fDarkF
                DESC   @fPlayer         

> _       _     PROCESS 3

; Jetzt verwenden wir Prozess 1 als Hauptspielschleife. Eine Rückkehr von
; hier bedeutet es das Ende des Spiels

> _       _     PROCESS 1

;------------------------------------------------------------------------------
/PRO 1
> _       _     PROCESS 4               ; Do process 2 stuff here

;> _       _     MINUS   5       1       ; Update auto flags
;                MINUS   6       1       ; These are not supported anymore
;                MINUS   7       1       ; as we use the low section as
;                MINUS   8       1       ; a small stack
;                NOTZERO 0               ; But should you need them
;                MINUS   9       1       ; this is the Version 1 DAAD code!
;                ABSENT  0
;                MINUS   10      1

> _       _     PARSE   0               ; Get next LS from current buffer
                PROCESS 2               ; Failed cos of invalid or timeout
                REDO

> _       _     EQ      fTurns   255     ; Max for one byte
                PLUS    fTurnsHi 1
                CLEAR   fTurns
                SKIP    1

> _       _     PLUS    fTurns   1

> _       _     PROCESS 5               ; Do any commands
                ISDONE                  ; Done something
                REDO

> _       _     MOVE    fPlayer          ; No so try to move player
                CLS
                RESTART                 ; Absolute jump to start process 0

> _       _     NEWTEXT
                LT      fVerb    14
                SYSMESS 7
                REDO

> _       _     SYSMESS 8
                REDO


;------------------------------------------------------------------------------
/PRO 2
> _       _     HASAT TIMEOUT
                SYSMESS 35
                DONE

> _       _     SYSMESS 6               ; Ich habe es nicht verstanden

;------------------------------------------------------------------------------
/PRO 3 ; Alter Prozess 1. Beachten Sie, dass sowohl die Antworttabelle als auch die alte PAW
; Die Prozesstabellen 1 und 2 können nun irgendwo vorhanden sein oder ganz fehlen. Alles
; ist in der DAAD-Sprache selbst implementiert.

;This is better carried out thus
> _       _     NEWLINE
                ZERO    fDarkF            ; Isn't dark
                LISTOBJ

;than the old system needed in Version 1, without accesss to DarkF
;> _       _     NEWLINE
;                ZERO    0               ;If it is light...
;                ABSENT  0               ;and the light source is absent...
;                LISTOBJ                 ;List the objects

;> _       _     PRESENT 0               ;If the light source is present...
;                LISTOBJ                 ;List the objects

;------------------------------------------------------------------------------
/PRO 4 ; Old process 2
;------------------------------------------------------------------------------
/PRO 5 ; Command decoder

> I       _       SYSMESS 9
                  LISTAT  CARRIED
                  SYSMESS 10
                  LISTAT  WORN
                  DONE

> NIMM    ALLES   DOALL   HERE
> NIMM    _       AUTOG
                  DONE

> LEGE    ALLES   DOALL   CARRIED
> LEGE    _       AUTOD
                  DONE

> ENTFE   ALLES   DOALL   WORN
> ENTFE   _       AUTOR
                  DONE

> TRAGE   ALLES   DOALL   CARRIED
> TRAGE   _       AUTOW
                  DONE

> B       _       CLS
                  RESTART

> ENDE    _       QUIT
                  END

> ENDE    _       DONE                    ;Not like QUIT on PAW

> SPEIC    _     #ifndef "tape"
                XSAVE 0                  ;ditto
                #endif
                #ifdef "tape"
                SAVE 0
                #endif
                CLS
                RESTART

> LADE    _     #ifndef "tape"
                XLOAD  0
                #endif
                #ifdef "tape"
                LOAD   0
                #endif
                CLS
                RESTART

> RAMSP   _     RAMSAVE
                CLS
                RESTART

> RAMLA   _     RAMLOAD 255             ;Reload all flags
                CLS
                RESTART

> SCHAU   _     CLS
                RESTART
;------------------------------------------------------------------------------
/PRO 6 ; Initialise the DAAD system

> _       _     #ifdef "pc"
                    PAPER 0
                    INK 15
                #endif
                CLS
                WINDOW  1               ; Halbes Textfenster
                WINAT 14 0
                WINSIZE 25 128
                WINDOW   0
                CLS
                #ifdef "nativeraster"     ; Ortsdiagramm 0
                    PICTURE 0 
                    DISPLAY 0
                    SKIP $picture0OK
                #else 
                    #ifndef "tape48"
                        CLEAR 20
                        XPICTURE 0
                        ZERO 20
                        SKIP $picture0OK
                    #endif    
                #endif

#ifndef "splitModeON"                  
>  _     _      WINDOW  1                  ; Als wir hier ankamen, war die 000-Grafik nicht vorhanden
                WINAT 0 0              
                WINSIZE 25 128             ; Wir stellen den Bildschirm auf Vollbild ein, es sei denn, es handelt sich um einen Split-Modus
#endif
$picture0OK

> _       _     WINDOW 1
                DESC    0               ; Texteinführung
                ANYKEY
                CLS
                SET   255             

$initLoop                
> _       _     MINUS   255 1          ; Setzt alle Flags zurück
                NOTEQ   255  fGFlags  ; außer GFlags!!!
                CLEAR   @255

> _       _     NOTZERO 255             
                SKIP    $initLoop

> _       _     RESET                     ; Objekte an ihrem ursprünglichen Standort / Flag 1
                LET     fStrength 10
                LET     fMaxCarr 4
                SET     fCPronounNoun
                SET     fCPronounAdject
                ; LET fObjectsCarried   0 ; Initialisieren Sie diese Flagge mit der Anzahl der Gegenstände, die der Startspieler trägt
                GOTO    1                 ; Erster Ort im Spiel
/END